package it.unito.di.sarlexceptions.examples.treatment

import io.sarl.api.core.Behaviors
import io.sarl.api.core.DefaultContextInteractions
import io.sarl.api.core.Initialize
import io.sarl.api.core.Lifecycle
import io.sarl.api.core.Logging
import io.sarl.api.core.OpenEventSpace
import io.sarl.api.core.OpenEventSpaceSpecification
import it.unito.di.sarlexceptions.events.ExceptionSpaceCreated
import it.unito.di.sarlexceptions.spaces.ExceptionSpace
import it.unito.di.sarlexceptions.spaces.ExceptionSpaceSpecification
import java.util.UUID
import java.util.ArrayList
import io.sarl.api.core.AgentKilled

agent Pharmacist {
	
uses DefaultContextInteractions, Behaviors, Lifecycle, Logging

	var docSpace : OpenEventSpace

	var Authorization = new ArrayList<String>()

	var exSpacePharmacist : ExceptionSpace<Remind>

	var exSpaceTerminate : ExceptionSpace<Terminate>	
	
	on Initialize {
		docSpace = defaultContext.getOrCreateSpaceWithSpec(typeof(OpenEventSpaceSpecification),
			occurrence.parameters.get(0) as UUID)
	
		docSpace.registerStrongParticipant(asEventListener())

		val type = new ExceptionSpaceSpecification.class as Class<ExceptionSpaceSpecification<Remind>>
		exSpacePharmacist = defaultContext.getOrCreateSpaceWithID(type, UUID::randomUUID)
		exSpacePharmacist.registerAsRaiser(asEventListener)
		info("Pharmacist, Registered as raiser for Remind")
		emit(new ExceptionSpaceCreated(Remind, exSpacePharmacist.spaceID.ID))
	}
	
	on Prescription {
		if (occurrence.index == null){
			exSpacePharmacist.raiseException(new Remind(occurrence.source.getID), ID)
		}
		else {
			Authorization = occurrence.index
			Authorization.remove(1)
			info("Farmacista invia la prescrizione al paziente")
			emit(new FilledRx(Authorization))
			killMe
		}
	}

	on ExceptionSpaceCreated [occurrence.ex == Terminate] {
		exSpaceTerminate= defaultContext.getSpace(occurrence.id)
		exSpaceTerminate.registerAsHandler(asEventListener)
		info("Pharmacist, Registered as handler for Terminate")
		}
	on Terminate {
		killMe
	}

	on AgentKilled {
		if(docSpace.getNumberOfStrongParticipants == 1){
			killMe
		}
	}

}